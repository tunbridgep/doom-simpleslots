//This handles most aspects of the player slot system

struct IgnoreList
{
    Array<String> ignores;
    
    void Populate()
    {
        string convar = "slots_ignore";
        string list = CVar.GetCVar(convar,players[consoleplayer]).GetString();
        list.Split(ignores, ";", TOK_SKIPEMPTY);

        SlotDebug.LogVerbose("Populated Ignore List with "..ignores.Size().." items");
    }
    
    void Write()
    {
        string convar = "slots_ignore";

        string convar_write;
        for (int i = 0; i < ignores.Size(); i++)
        {
            convar_write.AppendFormat(ignores[i]..';');
        }

        SlotDebug.LogVerbose("Writing Ignore List ("..convar_write..") to " .. convar);
        CVar writevar = CVar.FindCVar(convar); //For some reason this doesn't work (not null, just doesn't write) when we use GetCVar()...
        writevar.SetString(convar_write);
    }


    bool IsIgnored(String item)
    {
        return ignores.Find(item) != ignores.Size() || item == "SlotsHolster";
    }

    void UpdateIgnore(String item)
    {
        let position = ignores.Find(item);
        if (position == ignores.Size())
        {
            SlotDebug.LogVerbose("Ignoring " .. item);
            ignores.Push(item);
        }
        else
        {
            SlotDebug.LogVerbose("No longer Ignoring " .. item);
            ignores.Delete(position);
        }
        Write();
    }
}

struct SlotManager
{
    Array<String> slots[10];

    void Populate()
    {
        for (int i = 0; i < slots.Size(); i++)
        {
            string convar = "slots_"..i.."_0";
            SlotDebug.LogVerbose("Populating Slots from "..convar);

            string list = CVar.GetCVar(convar,players[consoleplayer]).GetString();
            list.Split(slots[i], ";", TOK_SKIPEMPTY);
        }
        SlotDebug.LogVerbose("Populated Slots List");
    }

    void WriteSlot(int slot)
    {
        int total_convars = 5;
        string convar = "slots_"..slot.."_0";

        string convar_write;
        for (int i = 0; i < slots[slot].Size(); i++)
        {
            convar_write = convar_write..slots[slot][i]..';';
        }

        SlotDebug.LogVerbose("Writing Slot List ("..convar_write..") to " .. convar);
        CVar writevar = CVar.FindCVar(convar); //For some reason this doesn't work (not null, just doesn't write) when we use GetCVar()...
        writevar.SetString(convar_write);

    }

    void UpdateSlotItem(int slot, String item)
    {
        let position = slots[slot].Find(item);

        if (position == slots[slot].Size()) //If it's not in slot, push it
        {
            slots[slot].Push(item);
            Console.printf(item.." Added.");
        }
        else if (position < slots[slot].Size() - 1) //It's low priority, bump it to the top
        {
            slots[slot].Delete(position);
            slots[slot].Push(item);
            Console.printf(item.." is Low Priority, increasing.");
        }
        else //it's highest priority, remove it
        {
            slots[slot].Pop();
            Console.printf(item.." Removed.");
        }
        WriteSlot(slot);
    }

    Name GetSlotItem(int slot)
    {
        let item = 'None';
        let size = slots[slot].Size();
        if (size > 0)
        {
            for (int i = 0;i < size;i++)
            {
                if (ValidWeapon(slots[slot][i]))
                    item = slots[slot][i];
            }
        }
        
        return item;
    }

    bool ValidWeapon(Name weapon)
    {
        let Weap = Weapon(players[consoleplayer].mo.FindInventory(weapon));
        return Weap != null;
    }

    /*
    private void GetPlayerWeapons()
    {
        for (int i = 0; i < 9;i++)
        {
            for (int j = 0;j < player.weapons.SlotSize(i))
            {
                let slotweaps = player.weapons.GetWeapon(i,j);
            }
        }
    }
    */
}

class SlotsToken : Inventory
{
    Default
    {
        +INVENTORY.UNDROPPABLE;
        +INVENTORY.ALWAYSPICKUP;
        +INVENTORY.UNCLEARABLE;
        +INVENTORY.HUBPOWER;
        +INVENTORY.PERSISTENTPOWER;
        +INVENTORY.UNTOSSABLE;
        +INVENTORY.IGNORESKILL;
        //+INVENTORY.KEEPDEPLETED;
        //+INVENTORY.INVBAR;
        Inventory.InterHubAmount 1;
        Inventory.MaxAmount 1;
    }

    Name actualCurrentWeapon; //our ACTUAL currently selected weapon, used to know when we change weapon
    Name lastWeapon; //The previous weapon we selected which was not ignored
    Name currWeapon; //The latest weapon we selected which was not ignored
    Name ignored; //Name of the currently ignored weapon. This is temporary and not added to the ignore list
    Name goodWeapon; //Name of the currently selected weapon if it's valid for being a lastWeapon but hasn't been set yet.
    SlotManager slots;
    IgnoreList ignores;
    bool poweredUp; //Is our weapon a powered-up version?
    bool enableRelease; //Can we release a slot button
    float timer;

    override void AttachToOwner(Actor other)
    {
        Super.AttachToOwner(other);
        DoPopulate();
    }

    void DoPopulate()
    {
        slots.Populate();
        ignores.Populate();
    }

    override void DoEffect()
    {
        timer += 1.0 / TICRATE;
        //SlotDebug.LogVerbose("Timer: " .. timer);
        Super.DoEffect();
        RememberWeapons();
        if (goodWeapon != "" && timer >= slots_delay)
        {
            SetLastWeapon();
        }
    }

    private bool IsPending()
    {
        let pend = Owner.Player.PendingWeapon;
        return pend != WP_NOCHANGE;
    }

    private Name GetWeapon()
    {
        let pend = Owner.Player.PendingWeapon;
        let weap = Owner.Player.ReadyWeapon;
        if (pend != WP_NOCHANGE)
        {
            poweredUp = pend.bPOWERED_UP;
            return pend.GetClassName();
        }
        else if (weap)
        {
            poweredUp = weap.bPOWERED_UP;
            return weap.GetClassName();
        }
        return 'None';
    }

    private void SwitchWeapon(Name newWeapon)
    {
        enableRelease = false;
        owner.A_SelectWeapon(newWeapon);
        SlotDebug.LogVerbose("Switching Weapon to: "..newWeapon);
        if (newWeapon != "SlotsHolster")
            owner.TakeInventory("SlotsHolster",9999);
    }
    
    private bool CurrValid()
    {
        return owner.FindInventory(currWeapon) != null;
    }

    private void RememberWeapons()
    {
        let weap = GetWeapon();
        if (actualCurrentWeapon != weap && weap != currWeapon && weap != 'None' && !poweredUp) //changed weapon
        {
            goodWeapon = "";
            if (ignored == weap)
            {
                ignored = 'None';
                SlotDebug.LogVerbose("Weapon Switch Ignored (Temp Ignore)");
            }
            else if (currWeapon == 'None' || !CurrValid())
            {
                currWeapon = weap;
                SlotDebug.LogVerbose("No Curr Weapon Set. Setting to "..weap);
            }
            else if (ignores.IsIgnored(weap))
            {
                SlotDebug.LogVerbose("Weapon Switch Ignored (Ignore List)");
            }
            else if (weap == lastWeapon)
            {
                goodWeapon = weap;
                SetLastWeapon();
            }
            else
            {
                //Can't set it here, we have to wait for the timer to complete.
                //So, just set our goodWeapon to let the timer know it's good to change lastweapon.
                goodWeapon = weap;
            }
            timer = 0;
            actualCurrentWeapon = weap;
        }
    }

    //Sets the lastweapon to our latest valid goodWeapon
    void SetLastWeapon()
    {
        lastWeapon = currWeapon;
        currWeapon = goodWeapon;
        goodWeapon = "";
        SlotDebug.LogVerbose("GoodWeapon set, setting lastWeapon because timer has expired.");
    }

    //Config a slot
    void DoConfigSlot(int slot)
    {
        SlotDebug.LogVerbose("slot "..slot.." config press");
        slots.UpdateSlotItem(slot,GetWeapon());
        enableRelease = false;
    }

    //Selecting a slot
    void DoWeaponSlot(int slot)
    {
        let weap = GetWeapon();
        let slotweap = slots.GetSlotItem(slot);
        if (slotweap == 'None')
        {
            SlotDebug.LogVerbose("No Weapon in Slot");
            enableRelease = false;
        }
        else if (slotweap != weap)
        {
            SlotDebug.LogVerbose("slot "..slot.." press. Selecting " .. slotweap);
            ignored = slotweap;
            SwitchWeapon(slotweap);
            enableRelease = IsPending();
            SlotDebug.LogVerbose("enableRelease: " .. enableRelease);
        }
        else
        {
            ignored = 'None';
            SwitchWeapon(currWeapon);
            SlotDebug.LogVerbose("Switching back to previous weapon");
        }
    }

    //Releasing slot button
    void DoWeaponSlotRelease()
    {
        if (!IsPending() && enableRelease)
            DoLastWeapon();
        enableRelease = true;
    }

    //Selecting last weapon
    void DoLastWeapon()
    {
        SlotDebug.LogVerbose("last weapon press");
        
        let weap = GetWeapon();
        if (weap != currWeapon && !poweredUp)
            SwitchWeapon(currWeapon);
        else if (lastWeapon != 'None')
            SwitchWeapon(lastWeapon);
    }

    //Ignore our current weapon
    void DoIgnoreWeapon()
    {
        SlotDebug.LogVerbose("Ignoring Weapon");
        ignores.UpdateIgnore(GetWeapon());
    }

    //Handle Holstering
    void DoHolster()
    {
        let weap = GetWeapon();
        let holster = "SlotsHolster";
        let holsterItem = SlotsHolster(owner.FindInventory(holster));

        if (weap == holster)
        {
            ignored = holsterItem.GetLastWeapon();
            SwitchWeapon(holsterItem.GetLastWeapon());
        }
        else
        {
            if (holsterItem == null)
            {
                owner.GiveInventory(holster,1);
                holsterItem = SlotsHolster(owner.FindInventory(holster));
            }

            ignored = holster;
            let prev = actualCurrentWeapon == "SlotsHolster" ? currWeapon : actualCurrentWeapon;
            holsterItem.SetLastWeapon(prev);
            SwitchWeapon(holster);
            ignored = prev;
            SlotDebug.LogVerbose("Holstering Weapon");
        }
    }
}
