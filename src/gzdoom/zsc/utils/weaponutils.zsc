//Have to do this here because it's different for GZDoom and Selaco
class WeaponUtils
{
    static bool HasAmmo(Inventory weapon)
    {
        let p = players[consoleplayer].mo;
        let Weap = Weapon(p.FindInventory(weapon.GetClassName()));

        if (Weap != Null)
        {
            Ammo ammo1 = Ammo(p.FindInventory(Weap.AmmoType1));
            Ammo ammo2 = Ammo(p.FindInventory(Weap.AmmoType2));

            bool hasAmmo1 = !ammo1 || Weap.bAMMO_OPTIONAL || ammo1.Amount >= Weap.AmmoUse1;
            bool hasAmmo2 = !ammo2 || Weap.bALT_AMMO_OPTIONAL || ammo2.Amount >= Weap.AmmoUse2;

            return hasAmmo1 || hasAmmo2;
        }
        return false;
    }
}

class WeaponUtilsP play
{
    static void UpdateHolsterAmmo(Actor player, Inventory current)
    {
        Weapon curs = Weapon(current);
        if (!curs)
            return;

        if (curs.AmmoType1)
        {
            let ammo1 = player.CountInv(curs.AmmoType1.GetClassName());
            player.SetInventory("HolsterAmmo",ammo1);
        }
        else
            player.SetInventory("HolsterAmmo",0);

        if (curs.AmmoType2)
        {
            let ammo2 = player.CountInv(curs.AmmoType2.GetClassName());
            player.SetInventory("HolsterAmmo2",ammo2);
        }
        else
            player.SetInventory("HolsterAmmo2",0);
        
        //change ammo icon
        let a1 = player.FindInventory("HolsterAmmo");
        let a2 = player.FindInventory("HolsterAmmo2");

        if (a1 && curs.Ammo1)
            a1.Icon = curs.Ammo1.Icon;
        if (a2 && curs.Ammo2)
            a2.Icon = curs.Ammo2.Icon;
    }
}
