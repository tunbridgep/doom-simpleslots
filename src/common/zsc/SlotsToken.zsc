//This is responsible for converting a certain number of CVARS to and from arrays
//Which allows us to split one array into multiple cvars, to make sure we don't exceed Doom's CVAR limit
struct CvarContainer
{
    Array<string> values;
    String convar;
    int convars;

    void Populate(String cv, int num)
    {
        convar = cv;
        convars = num;
        values.Clear();
        for (int i = 0;i < num;i++)
        {
            string list = CVar.GetCVar(convar.."_"..i,players[consoleplayer]).GetString();
            list.Split(values, ";", TOK_SKIPEMPTY);
        }
    }
    
    void Write()
    {
        string write_text;

        int cv_count = 0;
        for (int i = 0; i < values.Size(); i++)
        {
            //If we try to write to the current cvar, make sure it's not too long
            //If it is, move to the next cvar
            if ((write_text..values[i]..';').CodePointCount() > 256)
            {
                WriteCvar(convar.."_"..cv_count,write_text);
                write_text = "";
                SlotDebug.LogVerbose("("..convar.."_"..cv_count..") cvar Full, Incrementing cv_count to "..cv_count);
                cv_count++;
            }
            
            if (cv_count >= convars)
                return;

            write_text.AppendFormat(values[i]..';');
        }
        WriteCvar(convar.."_"..cv_count,write_text);
    }

    private void WriteCvar(String cv, String convar_write)
    {
        //For some reason this doesn't work (not null, just doesn't write) when we use GetCVar()...
        CVar writevar = CVar.FindCVar(cv);
        writevar.SetString(convar_write);
    }

    String GetAt(int index)
    {
        return values[index];
    }

    int Find(String item)
    {
        return values.Find(item);
    }
    
    int Size()
    {
        return values.Size();
    }
    
    void Insert(uint index, String value)
    {
        values.Insert(index,value);
    }
    
    void Delete(uint index, int deleteCount = 1)
    {
        values.Delete(index,deleteCount);
    }

    int Push(String value)
    {
        return values.Push(value);
    }

    bool Pop()
    {
        return values.Pop();
    }
}

//This handles ignored weapons
struct IgnoreList
{
    CvarContainer ignores;
    
    void Populate()
    {
        ignores.Populate("slots_ignore",5);
        SlotDebug.LogVerbose("Populated Ignore List with "..ignores.values.Size().." items");
    }
    
    void Write()
    {
        ignores.Write();
        SlotDebug.LogVerbose("Writing Ignore List");
    }


    bool IsIgnored(Inventory item)
    {
        let nm = item.GetClassName();
        return ignores.Find(nm) != ignores.Size();
    }

    void UpdateIgnore(Inventory item)
    {
        let nm = item.GetClassName();
        let position = ignores.Find(nm);
        String text;
        if (position == ignores.Size())
        {
            text = Stringtable.Localize("$SLOT_IGNORED");
            SlotDebug.LogVerbose("Ignoring " .. nm);
            ignores.Push(nm);
        }
        else
        {
            text = Stringtable.Localize("$SLOT_UNIGNORED");
            SlotDebug.LogVerbose("No longer Ignoring " .. nm);
            ignores.Delete(position);
        }
        Write();
        Console.Printf(text,item.GetTag());
    }
}

//This handles most aspects of the player slot system
struct SlotManager
{
    const NUM_SLOTS = 10;
    CVarContainer slots[NUM_SLOTS];
    Actor owner;

    void Populate(Actor own)
    {
        owner = own;
        for (int i = 0; i < NUM_SLOTS; i++)
        {
            slots[i].Populate("slots_"..i,5);
        }
        SlotDebug.LogVerbose("Populated Slots List");
    }

    void WriteSlot(int slot)
    {
        slots[slot].Write();
        SlotDebug.LogVerbose("Writing Slot List (Slot "..slot..")");
    }

    void UpdateSlotItem(int slot, Inventory wpn)
    {
        String item = wpn.GetClassName();

        let position = slots[slot].Find(item);

        if (position == slots[slot].Size()) //If it's not in slot, push it
        {
            slots[slot].Push(item);
            Console.printf(Stringtable.Localize("$SLOT_ADDED"),wpn.GetTag(),slot+1);
        }
        else if (position < slots[slot].Size() - 1) //It's low priority, bump it to the top
        {
            slots[slot].Delete(position);
            slots[slot].Push(item);
            Console.printf(Stringtable.Localize("$SLOT_UPDATED"),wpn.GetTag(),slot+1);
        }
        else //it's highest priority, remove it
        {
            slots[slot].Pop();
            Console.printf(Stringtable.Localize("$SLOT_REMOVED"),wpn.GetTag(),slot+1);
        }
        WriteSlot(slot);
    }

    Inventory GetSlotItem(int slot)
    {
        Inventory re = null;
        let size = slots[slot].Size();
        if (size > 0)
        {
            for (int i = 0;i < size;i++)
            {
                let item = slots[slot].GetAt(i);
                if (ValidWeapon(item))
                {
                    let wp = Inventory(owner.FindInventory(item));
                    if (wp)
                        re = wp;
                }
            }
        }
        else
        {
            //SlotDebug.LogVerbose("No Weapon in Slot");
            Console.Printf(Stringtable.Localize("$SLOT_EMPTY"));
        }
        
        return re;
    }

    bool AnySlotContains(Inventory wpn)
    {
        let nm = wpn.GetClassName();
        for (int i = 0; i < NUM_SLOTS; i++)
        {
            if (slots[i].Find(nm) != slots[i].Size())
                return true;
        }
        return false;
    }

    private bool ValidWeapon(String weapon, bool ammoMessage = true)
    {
        let p = players[consoleplayer].mo;
        let Weap = Inventory(p.FindInventory(weapon));

        if (Weap)
        {
            if (slots_check_ammo)
            {
                if (!WeaponUtils.HasAmmo(Weap))
                {
                    if (ammoMessage)
                    {
                        let text = Stringtable.Localize("$SLOT_NO_AMMO");
                        Console.Printf(String.Format(text,Weap.GetTag()));
                    }
                    return false;
                }
            }
            return true;
        }

        return false;
    }
}

class SlotsToken : Inventory
{
    Default
    {
        +INVENTORY.UNDROPPABLE;
        +INVENTORY.ALWAYSPICKUP;
        +INVENTORY.UNCLEARABLE;
        +INVENTORY.HUBPOWER;
        +INVENTORY.PERSISTENTPOWER;
        +INVENTORY.UNTOSSABLE;
        +INVENTORY.IGNORESKILL;
        //+INVENTORY.KEEPDEPLETED;
        //+INVENTORY.INVBAR;
        Inventory.InterHubAmount 1;
        Inventory.MaxAmount 1;
    }

    Inventory actualCurrentWeapon; //our ACTUAL currently selected weapon, used to know when we change weapon
    Inventory lastWeapon; //The previous weapon we selected which was not ignored
    Inventory currWeapon; //The latest weapon we selected which was not ignored
    Inventory ignored; //The currently ignored weapon. This is temporary and not added to the ignore list
    Inventory goodWeapon; //The currently selected weapon if it's valid for being a lastWeapon but hasn't been set yet.
    SlotManager slots;
    IgnoreList ignores;
    bool enableRelease; //Can we release a slot button
    float timer;
    const HOLSTER_NAME = "SlotsHolster";

    override void AttachToOwner(Actor other)
    {
        Super.AttachToOwner(other);
        DoPopulate();
    }

    void DoPopulate()
    {
        slots.Populate(owner);
        ignores.Populate();
    }

    override void DoEffect()
    {
        timer += 1.0 / TICRATE;
        //SlotDebug.LogVerbose("Timer: " .. timer);
        Super.DoEffect();
        RememberWeapons();
        if (goodWeapon != null && timer >= slots_delay)
        {
            SetLastWeapon();
        }
    }

    private bool IsPending()
    {
        let pend = Owner.Player.PendingWeapon;
        return pend != WP_NOCHANGE;
    }

    /*
    //Loops through the players weapons, and finds the highest weapon
    //that it can, on whatever slot, to set as our default lastweapon
    private void SetStartingLastWeapon()
    {
        Inventory findLast;
        Inventory weap = owner.Player.PendingWeapon;
        for (int i = 0; i < 4;i++)
        {
            SlotDebug.LogVerbose("Checking Slot: "..i);
            for (int j = 0;j < owner.Player.weapons.SlotSize(i);j++)
            {
                let slotweap = Inventory(owner.FindInventory(owner.player.weapons.GetWeapon(i,j)));
                if (slotweap)
                {
                    SlotDebug.LogVerbose("Checking Slot Weapon: "..slotweap.GetClassName());
                    if (weap != slotweap)
                        findLast = slotweap;
                }
            }
        }

        lastWeapon = findLast;
    }
    */

    private Inventory GetWeapon()
    {
        let weap = owner.Player.ReadyWeapon;

        if (weap)
        {
            if (weap.bPOWERED_UP) //Handle Heretic Tome of Power Weapons
                return weap.SisterWeapon;
            else
                return weap;
        }
        return null;
    }

    private void SwitchWeapon(Inventory newWeapon)
    {
        let wp = newWeapon.GetClassName();
        enableRelease = false;
        owner.A_SelectWeapon(wp);
        SlotDebug.LogVerbose("Switching Weapon to: "..wp);
        if (wp != "SlotsHolster")
            owner.TakeInventory("SlotsHolster",9999);
    }
    
    private bool CurrValid()
    {
        return owner.FindInventory(currWeapon.GetClassName()) != null;
    }

    //Known bad weapons that should NEVER be allowed into any slot or last weapon selection for any reason
    private bool IsBadWeapon(Inventory weap)
    {
        let nm = weap.GetClassName();
        return nm == "SlotsHolster" || nm == "MotoHandles" || nm == "Holster" || nm == "Holsterweapon" || nm == "Lucyhandles" || nm == "Fire_Extinguisher";
    }

    private void RememberWeapons()
    {
        let weap = GetWeapon();
        if (actualCurrentWeapon != weap && weap != null) //changed weapon
        {
            goodWeapon = null;
            if (ignored == weap)
            {
                ignored = null;
                SlotDebug.LogVerbose("Weapon Switch Ignored (Temp Ignore)");
            }
            else if (weap == currWeapon)
            {
                //Do Nothing
            }
            else if (currWeapon == null || !CurrValid())
            {
                currWeapon = weap;
                SlotDebug.LogVerbose("No Curr Weapon Set. Setting to "..weap.GetClassName());
            }
            else if (IsBadWeapon(weap))
            {
                SlotDebug.LogVerbose("Weapon Switch Ignored (Bad Weapon)");
            }
            else if (ignores.IsIgnored(weap) && lastWeapon != null)
            {
                SlotDebug.LogVerbose("Weapon Switch Ignored (Ignore List)");
            }
            else if (slots.AnySlotContains(weap) && slots_auto_ignore_in_slots && lastWeapon != null)
            {
                SlotDebug.LogVerbose("Weapon Switch Ignored (Weapon in Slot)");
            }
            else if (weap == lastWeapon)
            {
                goodWeapon = weap;
                SetLastWeapon();
            }
            else
            {
                //Can't set it here, we have to wait for the timer to complete.
                //So, just set our goodWeapon to let the timer know it's good to change lastweapon.
                SlotDebug.LogVerbose("Setting goodWeapon to "..weap.GetClassName());
                goodWeapon = weap;
            }
            timer = 0;
            actualCurrentWeapon = weap;
        }
    }

    //Sets the lastweapon to our latest valid goodWeapon
    void SetLastWeapon()
    {
        lastWeapon = currWeapon;
        currWeapon = goodWeapon;
        goodWeapon = null;
        SlotDebug.LogVerbose("GoodWeapon set, setting lastWeapon because timer has expired.");
    }

    //Config a slot
    void DoConfigSlot(int slot)
    {
        SlotDebug.LogVerbose("slot "..slot.." config press");
        let weap = GetWeapon();
        if (!IsBadWeapon(weap))
        {
            slots.UpdateSlotItem(slot,GetWeapon());
            enableRelease = false;
        }
    }

    //Selecting a slot
    void DoWeaponSlot(int slot)
    {
        let weap = GetWeapon();
        let slotweap = slots.GetSlotItem(slot);
        if (slotweap == null)
        {
            enableRelease = false;
        }
        else if (slotweap == currWeapon && slotweap == actualCurrentWeapon)
        {
            Console.printf(Stringtable.Localize("$SLOT_ALREADY_SELECTED"));
            enableRelease = false;
        }
        else if (slotweap != weap)
        {
            SlotDebug.LogVerbose("slot "..slot.." press. Selecting " .. slotweap.GetClassName());
            ignored = slotweap;
            SwitchWeapon(slotweap);
            enableRelease = IsPending();
            SlotDebug.LogVerbose("enableRelease: " .. enableRelease);
        }
        else
        {
            ignored = null;
            SwitchWeapon(currWeapon);
            SlotDebug.LogVerbose("Switching back to previous weapon");
        }
    }

    //Releasing slot button
    void DoWeaponSlotRelease()
    {
        if (!IsPending() && enableRelease)
            DoLastWeapon();
        enableRelease = true;
    }

    //Selecting last weapon
    void DoLastWeapon()
    {
        SlotDebug.LogVerbose("last weapon press");
        
        let weap = GetWeapon();
        if (weap != currWeapon)
            SwitchWeapon(currWeapon);
        else if (lastWeapon != null)
            SwitchWeapon(lastWeapon);
    }

    //Ignore our current weapon
    void DoIgnoreWeapon()
    {
        let weap = GetWeapon();
        if (!IsBadWeapon(weap))
            ignores.UpdateIgnore(GetWeapon());
    }

    //Handle Holstering
    void DoHolster()
    {
        let weap = GetWeapon();
        let holster = SlotsHolster(owner.FindInventory(HOLSTER_NAME));

        if (weap == holster)
        {
            ignored = holster.GetLastWeapon();
            SwitchWeapon(holster.GetLastWeapon());
        }
        else
        {
            if (holster== null)
            {
                owner.GiveInventory(HOLSTER_NAME,1);
                holster = SlotsHolster(owner.FindInventory(HOLSTER_NAME));
            }

            ignored = holster;
            let prev = actualCurrentWeapon == holster ? currWeapon : actualCurrentWeapon;
            holster.SetLastWeapon(prev);
            SwitchWeapon(holster);
            ignored = prev;
            SlotDebug.LogVerbose("Holstering Weapon");
        }
    }
}
