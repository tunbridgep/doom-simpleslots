//We need to use Inventory for everything
//because Selaco doesn't use Weapon as it's base class
//for some insane and ridiculous reason.
mixin class SlotsHolsterInc
{
    Inventory lastWeapon;

    void SetLastWeapon(Inventory weapon)
    {
        lastWeapon = weapon;
    }

    Inventory GetLastWeapon()
    {
        return lastWeapon;
    }

    private action void A_Select()
    {
        invoker.UpdateCrosshair(slots_holster_crosshair);
        A_Raise(999);
    }
    
    private action void A_Deselect()
    {
        invoker.OnUnholster();
        A_Lower(999);
        invoker.UpdateCrosshair(0);
    }

    private action void A_Unholster()
    {
        if (invoker.lastWeapon)
            A_SelectWeapon(invoker.lastWeapon.GetClassName());
    }
    
    Default
    {
        +INVENTORY.UNDROPPABLE
        +INVENTORY.ALWAYSPICKUP
        +INVENTORY.HUBPOWER
        +INVENTORY.PERSISTENTPOWER
        +INVENTORY.UNTOSSABLE
        +INVENTORY.IGNORESKILL
        Tag "Weapon Holstered";
    }
    States
    {
        Ready:
            TNT1 A 1 A_WeaponReady;
            TNT1 A 1 A_SetCrosshair(-1);
            Loop;
        Select:
            TNT1 A 1 A_Select;
            Loop;
        Deselect:
            TNT1 A 1 A_Deselect;
            Loop;
        Unholster:
        Fire:
        AltFire:
            TNT1 A 1 A_Unholster;
            Goto Ready;
    }
}
